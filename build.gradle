plugins {
    id "org.springframework.boot" version "2.5.7"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "org.sonarqube" version "3.3"
    id "jacoco"
}

repositories {
    mavenCentral()
}

ext {
    jacocoVersion = "0.8.8"
}

allprojects {
    group = "store.streetvendor"
}

subprojects {
    apply {
        plugin("java-library")
        plugin("eclipse")
        plugin("org.springframework.boot")
        plugin("io.spring.dependency-management")
        plugin("jacoco")
        plugin("org.sonarqube")
    }

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    sourceCompatibility = "11"

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // Test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
        jacoco {
            destinationFile = file("${buildDir}/jacoco/test.exec")
        }
    }
}

jacoco {
    toolVersion = "${jacocoVersion}"
}

task testCoverage(type: JacocoReport, group: "verification") {
    executionData(fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec"))

    subprojects.forEach {
        sourceSets(it.sourceSets.main as SourceSet)
    }

    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "yerimkoko_street-vendor-backend"
        property "sonar.organization", "yerimkoko"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/testCoverage/testCoverage.xml"
    }
}
